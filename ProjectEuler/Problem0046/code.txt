/*
Problem 46
	It was proposed by Christian Goldbach that every odd composite number can be written as the sum of a prime and twice a square.

		9 = 7 + 2×1^2
		15 = 7 + 2×2^2
		21 = 3 + 2×3^2
		25 = 7 + 2×3^2
		27 = 19 + 2×2^2
		33 = 31 + 2×1^2

	It turns out that the conjecture was false.

	What is the smallest odd composite that cannot be written as the sum of a prime and twice a square?
*/

//Code
 problem46←{
     primes←{⍺←1↓⍳⍵ ⋄ 0=≢⍺:1↓⍵ ⋄ ({/⍨∘(~0=⊃|⊢)⍨⍵}⍺)∇ ⍵,⊃⍺}⍵
     squares←{2×2*⍨⍳⌊⍵*0.5}⍵
     odd_composites←primes~⍨1+2×⍳⌈⍵÷2
     p_plus_s←squares∘.+primes
     is_in_p_plus_s←odd_composites∊p_plus_s
     ∨/~is_in_p_plus_s:⊃odd_composites/⍨~is_in_p_plus_s
     ∇(⍵×2)
 } 9

/* Explanation
  problem46←{
     primes←{⍺←1↓⍳⍵ ⋄ 0=≢⍺:1↓⍵ ⋄ ({/⍨∘(~0=⊃|⊢)⍨⍵}⍺)∇ ⍵,⊃⍺}⍵ // Sieves primes up to ⍵
     squares←{2×2*⍨⍳⌊⍵*0.5}⍵ 									 // Generates squares and doubles them
     odd_composites←primes~⍨1+2×⍳⌈⍵÷2						     // Creates odd composites by generating odd numbers and removing all primes.
     p_plus_s←squares∘.+primes // Finds all combinations of squares and primes
     is_in_p_plus_s←~odd_composites∊p_plus_s // checks all odd composites for non-membership in the primes plus squares
     ∨/is_in_p_plus_s:⊃odd_composites/⍨is_in_p_plus_s  // If any are not present, return the smallest one.
     ∇(⍵×2) // If all were present, double the range checked
 } 9 // start with the first odd composite.

 */ 