/*
Problem 22:
	Using data.txt, a 46K text file containing over five-thousand first names, begin by sorting it into alphabetical order. Then working out the alphabetical value for each name, multiply this value by its alphabetical position in the list to obtain a name score.

	For example, when the list is sorted into alphabetical order, COLIN, which is worth 3 + 15 + 12 + 9 + 14 = 53, is the 938th name in the list. So, COLIN would obtain a score of 938 × 53 = 49714.

	What is the total of all the name scores in the file?

*/

//Code
data←⊃⊃⎕NGET'.\Problem0022\data.txt' 1
+/{⍵×⍳≢⍵}{+/⎕A⍳⍵}¨(⊂∘⍋⌷⊢){1↓¯1↓⍵}¨','(≠⊆⊢)data


/* Explanation:
data←⊃⊃⎕NGET'.\Problem0022\data.txt' 1 // Imports the names as a large character vector

+/{⍵×⍳≢⍵} // Multiples that value by its position in the vector and sums the result
{+/⎕A⍳⍵}¨ // Calculates the alphabetical value
(⊂∘⍋⌷⊢) // Sorts the names
{1↓¯1↓⍵}¨     // Removes the quotes around the names
','(≠⊆⊢)data // Splits the vector into a vector of names
*/