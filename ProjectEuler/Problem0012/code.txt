/*
Problem 12
	The sequence of triangle numbers is generated by adding the natural numbers. So the 7th triangle number would be 1 + 2 + 3 + 4 + 5 + 6 + 7 = 28. The first ten terms would be:

	1, 3, 6, 10, 15, 21, 28, 36, 45, 55, ...

	Let us list the factors of the first seven triangle numbers:

	 1: 1
	 3: 1,3
	 6: 1,2,3,6
	10: 1,2,5,10
	15: 1,3,5,15
	21: 1,3,7,21
	28: 1,2,4,7,14,28
	We can see that 28 is the first triangle number to have over five divisors.

	What is the value of the first triangle number to have over five hundred divisors?
*/

// Code
{⍺←2 ⋄ n←+/⍳⍺ ⋄ ⍵<(⊃×/(1+⊢)¨1↓⍉{⍺(≢⍵)}⌸{⍺←2 ⋄ 1=⊃⍵:1↓⍵ ⋄ 0=⍺|⊃⍵:⍺ ∇(((⊃⍵)÷⍺),1↓⍵,⍺) ⋄ 0≠⍺|⊃⍵:(⍺+1)∇ ⍵} n):n ⋄ (⍺+1)∇ ⍵}500


/* Explanation:
{
⍺←2 ⋄ // Initial ⍺th triangular number
n←+/⍳⍺ ⋄ // The actual triangular number at ⍺

	⍵<( // If divisors of triangular number exceed ⍵, then return it.
	⊃×/(1+⊢)¨1↓⍉		// Find the number of divisors by taking the exponents of the prime factorization, adding one and finding the product.
	{⍺(≢⍵)}⌸	// Gets the number of occurrences of each prime a.k.a. the exponents of the prime factors
		{ // Accumululates the prime factors of ⍵ by recursive trial division
			⍺←2 ⋄ // Starting at 2
			1=⊃⍵:1↓⍵ ⋄  // If the first element of ⍵ (our value beng factored) equals 1, stop and return the factors
			0=⍺|⊃⍵:⍺ ∇(((⊃⍵)÷⍺),1↓⍵,⍺) ⋄ // If ⍺ is a factor, divide ⍵ by ⍺, and append ⍺ to the end of the factors 
			0≠⍺|⊃⍵:(⍺+1)∇ ⍵} // If ⍺ is not a factor, increment ⍺ and repeat.
	 n):n ⋄

(⍺+1)∇ ⍵ 		// Recursive iteration
} 
500 
*/